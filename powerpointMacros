' from http://www.pptfaq.com/FAQ00820_Show_me_a_list_of_designs_and_which_slides_use_them.htm

Sub EnumDesigns()
    Dim lCtrA As Integer
    Dim oPres As Presentation
    Set oPres = ActivePresentation
    With oPres
        Debug.Print "Number of applied templates: " & .Designs.Count
        For lCtrA = 1 To .Designs.Count
            Debug.Print "Template Design name: " & .Designs(lCtrA).Name
            Debug.Print vbTab & "Slide master name: " & .Designs(lCtrA).SlideMaster.Name
            
            If .Designs(lCtrA).HasTitleMaster Then
                Debug.Print vbTab & "Title master name: " & .Designs(lCtrA).TitleMaster.Name
            Else
                Debug.Print vbTab & "No Title master present"
            End If
        Next lCtrA
    End With
End Sub

Sub EnumCustomLayouts()
Dim oDesign As Design
Dim oCL As CustomLayout
Dim lngLayoutCount As Long
Dim I As Integer
For Each oDesign In ActivePresentation.Designs
    lngLayoutCount = oDesign.SlideMaster.CustomLayouts.Count
    Debug.Print "Total number of custom layouts in design " & Chr(34) & _
oDesign.Name & Chr(34) & ": " & lngLayoutCount
    'Enumerate all the available layouts
For I = 1 To lngLayoutCount
        Set oCL = oDesign.SlideMaster.CustomLayouts(I)
        Debug.Print "Name of layout: " & oCL.Name
    Next
Next
End Sub
 
Sub ListDesigns()
  Dim oPres As Presentation
  Dim oSld As Slide
  Dim oDes As Design
  Dim sText As String
  Dim FileNum As Integer
  Dim PathSep As String

  #If Mac Then
      PathSep = ":"
  #Else
      PathSep = "\"
  #End If

  Set oPres = ActivePresentation

  sText = "=== DESIGNS in this presentation ===" & vbCrLf

  For Each oDes In oPres.Designs
     sText = sText & oDes.Name & vbCrLf
  Next

  sText = sText & "=== SLIDES and the Designs they rode in on ===" & vbCrLf

  For Each oSld In oPres.Slides
     sText = sText & "Slide:" & vbTab & oSld.SlideIndex _
     & vbTab & oSld.Design.Name & vbTab & oSld.CustomLayout.Name & vbCrLf
  Next

   FileNum = FreeFile
  Open oPres.Path & PathSep & "REPORT.TXT" For Output As FileNum
  Print #FileNum, sText
  Close FileNum

  'MsgBox "Your report is saved as:" & vbCrLf _
  '              & oPres.Path & PathSep & "REPORT.TXT"

  #If Mac Then
    'MsgBox "Your report is saved as:" & vbCRLF _
                & oPres.Path & PathSep & "REPORT.TXT"
  #Else
    Call Shell("Notepad.exe" & " " & oPres.Path & PathSep & "REPORT.TXT", vbNormalFocus)
  #End If

   Set oPres = Nothing

End Sub

